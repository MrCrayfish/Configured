import net.neoforged.gradle.common.tasks.PotentiallySignJar

plugins {
    id 'multiloader-loader'
    id 'net.neoforged.gradle.userdev' version '7.0.107'
}

def at = file('src/main/resources/META-INF/accesstransformer.cfg')
if (at.exists()) {
    minecraft.accessTransformers.file at
}

sourceSets {
    test {
        runs {
            modIdentifier 'configuredTest'
        }
    }
}

runs {
    configureEach {
        modSource project.sourceSets.main
        modSource project.sourceSets.test
    }
    client {
        systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
    }
    server {
        systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        programArgument '--nogui'
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    implementation "net.neoforged:neoforge:${neoforge_version}"
    implementation "com.mrcrayfish:framework-neoforge:${minecraft_version}-${framework_version}"

    // JEI TODO temp until 1.20.5 is available
    compileOnly "mezz.jei:jei-1.20.4-common-api:${jei_version}"
    testCompileOnly "mezz.jei:jei-1.20.4-common-api:${jei_version}"
}

tasks.register('signJar', PotentiallySignJar) {
    dependsOn jar
    onlyIf {
        hasProperty('keyStore') || System.getenv("KEYSTORE")
    }
    keyStore = findProperty('keyStore') ?: System.getenv("KEYSTORE")
    alias = findProperty('keyStoreAlias') ?: System.getenv("KEYSTORE_ALIAS")
    storePass = findProperty('keyStorePass') ?: System.getenv("KEYSTORE_PASS")
    input = jar.archiveFile
}

jar.finalizedBy 'signJar'
