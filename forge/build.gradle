import net.minecraftforge.gradle.common.tasks.SignJar
import org.apache.commons.io.FilenameUtils

plugins {
    id 'multiloader-loader'
    id 'net.minecraftforge.gradle' version '[6.0.24,6.2)'
    id 'org.spongepowered.mixin' version '0.7-SNAPSHOT'
}

sourceSets {
    configuredTest {
        compileClasspath += main.compileClasspath
        runtimeClasspath += main.runtimeClasspath
        compileClasspath += test.compileClasspath
        runtimeClasspath += test.runtimeClasspath
    }
}

minecraft {
    mappings channel: 'official', version: minecraft_version
    reobf = false
    copyIdeResources = true

    def at = file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers = at
    }

    runs {
        configureEach {
            workingDirectory project.file('run')
            ideaModule "${rootProject.name}.${project.name}.configuredTest"
            property 'forge.logging.markers', 'REGISTRIES,SCAN,LOADING'
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            mods {
                configured {
                    source sourceSets.main
                    source sourceSets.test
                }
            }
        }
        client {
            taskName "Client"
            property 'forge.enabledGameTestNamespaces', mod_id
        }
        server {
            taskName "Server"
            property 'forge.enabledGameTestNamespaces', mod_id
            args '--nogui'
        }
    }
}

sourceSets.main.resources.srcDir 'src/generated/resources'

// Fixes an issue where net.sf.jopt-simple:jopt-simple cannot be resolved
configurations.configureEach {
    resolutionStrategy {
        it.force 'net.sf.jopt-simple:jopt-simple:5.0.4'
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    implementation "com.mrcrayfish:framework-forge:${minecraft_version}-${framework_version}"

    // JEI TODO temp until 1.20.5 is available
    compileOnly "mezz.jei:jei-1.20.4-common-api:${jei_version}"
    testCompileOnly "mezz.jei:jei-1.20.4-common-api:${jei_version}"
    annotationProcessor("org.spongepowered:mixin:0.8.5-SNAPSHOT:processor")

    // FROM FORGE: Hack fix for now, force jopt-simple to be exactly 5.0.4 because Mojang ships that version, but some transtive dependencies request 6.0+
    implementation('net.sf.jopt-simple:jopt-simple:5.0.4') { version { strictly '5.0.4' } }
}

tasks.register('signJar', SignJar) {
    dependsOn jar
    onlyIf {
        project.hasProperty('keyStore') || System.getenv("KEYSTORE")
    }
    keyStore = findProperty('keyStore') ?: System.getenv("KEYSTORE")
    alias = findProperty('keyStoreAlias') ?: System.getenv("KEYSTORE_ALIAS")
    storePass = findProperty('keyStorePass') ?: System.getenv("KEYSTORE_PASS")
    inputFile = jar.archiveFile
    def name = FilenameUtils.removeExtension(jar.archiveFile.get().getAsFile().getName());
    outputFile = layout.buildDirectory.file("libs/%s-signed.jar".formatted(name));
}

jar.finalizedBy 'signJar'

sourceSets.each {
    def dir = layout.buildDirectory.dir("sourcesSets/$it.name")
    it.output.resourcesDir = dir
    it.java.destinationDirectory = dir
}
